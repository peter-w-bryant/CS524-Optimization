{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Homework \\#6\n",
    "Due __Monday, August 8__ @ 11:59pm\n",
    "\n",
    "## Submission requirements\n",
    "Upload a **single PDF or HTML file** of your IJulia notebook for this entire assigment. Clearly denote which question each section of your PDF corresponds to."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Problem 1 -- Facility Location + Logic\n",
    "\n",
    "The 524 Bakery is expanding their operations beyond Wisconsin to Minnesota, Iowa, and Michigan. It is estimated that weekly demand for their most popular cake (Vanilla Buttercream) in each state will be 750 in Wisconsin, 300 in Minnesota, 450 in Iowa, and 600 in Michigan. The bakery is opening warehouses to mass produce the cakes in four cities: Madison, Minneapolis, Dubuque, and Ann Arbor. The average combined cost per cake of baking, packing, and shipping it from a warehouse to destinations in each state is given in the table below.\n",
    "\n",
    "|City| Wisconsin|Minnesota|Iowa|Michigan|\n",
    "|---:|---------:|--------:|---:|-------:|\n",
    "|Madison|75|92|84|102|\n",
    "|Minneapolis|96|105|80|114|\n",
    "|Dubuque|90|84|82|95|\n",
    "|Ann Arbor|106|102|112|85|\n",
    "\n",
    "Any warehouse can produce up to 1300 cakes per week. If a warehouse is opened, it incurs a fixed cost of \\$20,000 when opened. Demand in a state can be met from any combination of open warehouses. However, to make the hassle of shipping \"worth it,\" if any cakes are sent from a warehouse to a state, then at least 300 cakes must be sent from that warehouse to that state.\n",
    "\n",
    "(a) Formulate an integer program __using math notation__ that will help the bakery determine where to locate their warehouses and how many cakes should be sent from each warehouse to each state in order to minimize the total cost of meeting weekly demand.\n",
    "\n",
    "(b) Implement and solve the model from part(a) using the JuMP package in Julia, and explain the solution in terms of the problem. You should use Gurobi or Cbc as your solver.\n",
    "\n",
    "(c) Now suppose the bakery wishes to enforce the following constraint: if at least 600 cakes are made in Madison, then the amount shipped from Madison to Minnesota must be at least as much as the amount shipped from Madison to all other states. Extend the model to include this constraint. How does your solution change?\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solution Problem 1\n",
    "\n",
    "(a) Formulate an integer program using math notation that will help the bakery determine where to locate their warehouses and how many cakes should be sent from each warehouse to each state in order to minimize the total cost of meeting weekly demand.\n",
    "### Solution\n",
    "\n",
    "#### Sets \n",
    "\n",
    "$I$: set of cities $\\{$madison (1), minneapolis (2), dubuque (3), annarbor (4)$\\}$\n",
    "\n",
    "$J$: set of states $\\{$wi (1), mn (2), ia (3), mi (4)$\\}$\n",
    "\n",
    "$A$: set of all arcs between $i\\in I$ and $j\\in J$.\n",
    "\n",
    "#### Parameters \n",
    "\n",
    "$c_{ij}$: cost of producing and shipping a cake from $i\\in I$ \n",
    "to $j\\in J$.\n",
    "\n",
    "$d_j$: demand for cakes in state $j\\in J$. \n",
    "\n",
    "#### Variables \n",
    "\n",
    "$x_{ij}$: number of cakes shipped from warehouse $i \\in I$ to state $j \\in J$. \n",
    "\n",
    "$y_i$: Binary variable, equal to $1$ if a warehouse in $i\\in I$ is opened, $0$ otherwise.\n",
    "\n",
    "$z_{ij}$: Binary variable, equal to $1$ if any cakes are shipped from $i \\in I$ to state $j \\in J$, $0$ otherwise\n",
    "\n",
    "#### Objective \n",
    "\n",
    "Minimize the toatal cost:\n",
    "\\begin{align*}\n",
    "\\min \\quad &20000(y_1+y_2+y_3+y_4)+75x_{11}+92x_{12}+84x_{13}+102x_{14}\\\\\n",
    "&+96x_{21}+105x_{22}+80x_{23}+114x_{24}+90x_{31}+84x_{32}+82x_{33}\\\\\n",
    "&+95x_{34}+106x_{41}+102x_{42}+112x_{43}+85x_{44}\n",
    "\\end{align*}\n",
    "\n",
    "#### Constraints \n",
    "\n",
    "Demand in a state can be met by any combination of open warehouses:\n",
    "\\begin{align*}\n",
    "x_{11}+x_{21}+x_{31}+x_{41} \\geq 750\\\\\n",
    "x_{12}+x_{22}+x_{32}+x_{42} \\geq 300\\\\\n",
    "x_{13}+x_{23}+x_{33}+x_{43} \\geq 450\\\\\n",
    "x_{14}+x_{24}+x_{34}+x_{44} \\geq 600\n",
    "\\end{align*}\n",
    "\n",
    "Any open warehouse can produce up to 1300 cakes per week:\n",
    "\\begin{align*}\n",
    "x_{11}+x_{12}+x_{13}+x_{14} \\leq 1300y_1\\\\\n",
    "x_{21}+x_{22}+x_{23}+x_{24} \\leq 1300y_2\\\\\n",
    "x_{31}+x_{32}+x_{33}+x_{34} \\leq 1300y_3\\\\\n",
    "x_{41}+x_{42}+x_{43}+x_{44} \\leq 1300y_4\n",
    "\\end{align*}\n",
    "\n",
    "Turn off flow from warehouse $i \\in I$ to state $j \\in J$ if $z_{ij} = 0$, and enforce minimum flow if $z_{ij} = 1$:\n",
    "\n",
    "\\begin{align*} 300 z_{ij} \\leq x_{ij} \\leq 1300 z_{ij} \\quad \\forall i \\in I, j \\in J \\end{align*}\n",
    "\n",
    "Here we used the constant 1300 in the upper bound constraint since we know no warehouse can produce more than 1300 cakse (and hence at most that much can be shipped from the warehouse to any state). Smaller constants could be obtained by\n",
    "usng the demand of each state.\n",
    "\n",
    "\n",
    "Sign constraints and binay restrictions,\n",
    "\\begin{align*}\n",
    "&x_{ij} \\geq 0, \\quad \\forall (i,j)\\in A\\\\\n",
    "&z_{ij} \\in \\{0,1\\}, \\quad \\forall (i,j)\\in A\\\\\n",
    "&y_i \\in \\{0,1\\}, \\quad \\forall i\\in I\n",
    "\\end{align*}\n",
    "\n",
    "(b) Implement and solve the model from part(a) using the JuMP package in Julia, and explain the solution in terms of the problem. You should use Gurobi or Cbc as your solver.\n",
    "\n",
    "### Solution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "open warehouse in madison\n",
      "and ship 750.0 cakes to WI\n",
      "and ship 450.0 cakes to IA\n",
      "open warehouse in annarbor\n",
      "and ship 300.0 cakes to MN\n",
      "and ship 600.0 cakes to MI\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Cbc\n",
    "\n",
    "warehouse = [:madison, :minneapolis, :dubuque, :annarbor]\n",
    "states = [:WI, :MN, :IA, :MI]\n",
    "\n",
    "fixed_cost = 20000\n",
    "\n",
    "var_costs = [75 92 84 102 \n",
    "            96 105 80 114\n",
    "            90 84 82 95\n",
    "            106 102 112 85]\n",
    "\n",
    "using NamedArrays\n",
    "\n",
    "var_cost_NA = NamedArray(var_costs,(warehouse,states),(\"warehouse\",\"state\"))\n",
    "\n",
    "demand = Dict(zip(states,[750 300 450 600]))\n",
    "max_ship = 1300\n",
    "\n",
    "m = Model(Cbc.Optimizer)\n",
    "\n",
    "@variable(m, x[warehouse, states] >= 0)\n",
    "@variable(m, z[warehouse, states], Bin)\n",
    "@variable(m, y[warehouse], Bin)\n",
    "\n",
    "@objective(m, Min, sum(x[i,j]*var_cost_NA[i,j] for i in warehouse, j in states) + fixed_cost*sum(y))\n",
    "\n",
    "@constraint(m, meet_demand[i in states], sum(x[j,i] for j in warehouse) >= demand[i])\n",
    "@constraint(m, capacity[j in warehouse], sum(x[j,i] for i in states) <= max_ship*y[j])\n",
    "@constraint(m, lb[i in states, j in warehouse], 300z[j,i] <= x[j,i] )\n",
    "@constraint(m, ub[i in states, j in warehouse], x[j,i] <= 1300z[j,i])\n",
    "\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "for i in warehouse\n",
    "    if value(y[i]) > 0.99\n",
    "        println(\"open warehouse in \", i)\n",
    "        for j in states\n",
    "            if value(x[i,j]) > 0.001\n",
    "                println(\"and ship \", value(x[i,j]), \" cakes to \", j)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "(c) Now suppose the bakery wishes to enforce the following constraint: if at least 600 cakes are made in Madison, then the amount shipped from Madison to Minnesota must be at least as much as the amount shipped from Madison to all other states. Extend the model to include this constraint. How does your solution change?\n",
    "\n",
    "### Solution\n",
    "Introduce an auxiliary binary variable $\\delta$ and we model the logical chain:\n",
    "* $ \\sum_{j=1}^4 x_{1j} \\geq 600 \\Rightarrow \\delta = 1 \\Rightarrow x_{12} \\geq \\sum_{j=2}^4 x_{1j} $\n",
    "* For the first implication we use the last trick in the slide of tricks: $ \\sum_{j=1}^4 x_{1j} \\leq 600 - \\epsilon + (M+\\epsilon)\\delta $\n",
    "* If we assume cakes can only be sent in integer quantities, we can use $\\epsilon=1$, otherwise we use $\\epsilon = 0.01$. For $M$ we need an upper bound on $\\sum_{j=1}^4 x_{1j} - 600$. Since the total that can be made in Madison is limited to 1300 cakes, we can use $M=1300-600=700$. Thus, the first constraint is modeled with the linear constraint:$ \\sum_{j=1}^4 x_{1j} \\leq 699.99 + 700.01\\delta . $\n",
    "* For the second logical condition, we re-write it as: $ \\delta = 1 \\Rightarrow x_{11} - \\sum_{j=2}^4 x_{1j} \\geq 0 $\n",
    "* Using the third trick on the slide of tricks we have: $ x_{11} - \\sum_{j=2}^4 x_{1j} \\geq m (1-\\delta) $ where we need $m$ to be a lower bound on $x_{11} - \\sum_{j=2}^4 x_{1j}$. To derive this lower bound we know $x_{11} \\geq 0$, and $\\sum_{j=2}^4 x_{1j} \\leq 1300$ (the maximum possible production from Madison), thus we can use $m=-1300$.\n",
    "* Hence, the last constraint we add is:  $x_{11} - \\sum_{j=2}^4 x_{1j} \\geq -1300 (1-\\delta). $\n",
    "\n",
    "In summary, the extended model introduces the binary variable $\\delta$ and the two constraints:\n",
    "\\begin{align*}\n",
    " \\sum_{j=1}^4 x_{1j} &\\leq 699.99 + 700.01\\delta,  \\\\\n",
    " x_{12} - x_{11}-x{13}-x{14}&\\geq -1300 (1-\\delta). \n",
    "\\end{align*}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "open warehouse in dubuque\n",
      "and ship 450.00000000000006 cakes to WI\n",
      "and ship 300.0 cakes to MN\n",
      "and ship 450.0 cakes to IA\n",
      "open warehouse in annarbor\n",
      "and ship 300.0 cakes to WI\n",
      "and ship 600.0 cakes to MI\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Cbc\n",
    "\n",
    "states = [:WI, :MN, :IA, :MI]\n",
    "warehouse = [:madison, :minneapolis, :dubuque, :annarbor]\n",
    "\n",
    "fixed_cost = 20000\n",
    "\n",
    "var_costs = [75 92 84 102 \n",
    "            96 105 80 114\n",
    "            90 84 82 95\n",
    "            106 102 112 85]\n",
    "\n",
    "using NamedArrays\n",
    "\n",
    "var_cost_NA = NamedArray(var_costs,(warehouse,states),(\"warehouse\",\"state\"))\n",
    "\n",
    "demand = Dict(zip(states,[750 300 450 600]))\n",
    "max_ship = 1300\n",
    "\n",
    "m = Model(Cbc.Optimizer)\n",
    "\n",
    "@variable(m, x[warehouse, states] >= 0)\n",
    "@variable(m, z[warehouse, states], Bin)\n",
    "@variable(m, y[warehouse], Bin)\n",
    "@variable(m, δ, Bin)\n",
    "\n",
    "@objective(m, Min, sum(x[i,j]*var_cost_NA[i,j] for i in warehouse, j in states) + fixed_cost*sum(y))\n",
    "\n",
    "@constraint(m, meet_demand[i in states], sum(x[j,i] for j in warehouse) >= demand[i])\n",
    "@constraint(m, capacity[j in warehouse], sum(x[j,i] for i in states) <= max_ship*y[j])\n",
    "@constraint(m, lb[i in states, j in warehouse], 300z[j,i] <= x[j,i] )\n",
    "@constraint(m, ub[i in states, j in warehouse], x[j,i] <= 1300z[j,i])\n",
    "\n",
    "@constraint(m, sum(x[:madison,j] for j in states) <= 699.99 + 700.01δ)\n",
    "@constraint(m, x[:madison, :MN] - x[:madison, :WI] - x[:madison, :IA] - x[:madison, :MI] >= -1300(1-δ))\n",
    "\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "for i in warehouse\n",
    "    if value(y[i]) > 0.99\n",
    "        println(\"open warehouse in \", i)\n",
    "        for j in states\n",
    "            if value(x[i,j]) > 0.001\n",
    "                println(\"and ship \", value(x[i,j]), \" cakes to \", j)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Problem 2 -- Even *math* is political?\n",
    "\n",
    "\n",
    "Governor Gary Mander of the state of \"Win\"sconsin is redrawing the state's congressional districts (gerrymandering) so that he can ensure his preferred congressional candidates win every election. The state consists of ten cities, and the numbers of registered members of Party 1 and Party 2 (in thousands) in each city are shown below\n",
    "\n",
    "|City| Party 1|Party 2|\n",
    "|:---|:-------|:-----|\n",
    "|1|80|34|\n",
    "|2|60|44|\n",
    "|3|40|44|\n",
    "|4|20|24|\n",
    "|5|40|114|\n",
    "|6|40|64|\n",
    "|7|70|14|\n",
    "|8|50|44|\n",
    "|9|70|54|\n",
    "|10|70|64|\n",
    "\n",
    "\n",
    "Winsconsin has 5 congressional representatives, meaning these 10 cities are divided up into 5 different districts, and each district elects one representative. There are some restrictions on how these districts can be formed:\n",
    "* All voters in a city must belong to the same district\n",
    "* Each district must constain between 150,000 and 250,000 voters\n",
    "\n",
    "Governer Mander is a member of Party 1. Make the simplifying assumption that every voter will vote for the party they are registered to, regardless of the candidate. In other words, there are no \"independent\" voters in Winsconsin.\n",
    "\n",
    "(a) Formulate and solve an integer programming model to help Governer Mander maximize the number of representatives elected from Party 1. Assume that representatives are elected by simple majority, meaning that if a district contains at more Party 1 voters than Party 2 voters, a Party 1 representative is elected.\n",
    "\n",
    "Hints to get you started:\n",
    " * Create binary variables that assign each city $i = 1, 2,...,10$ to one of the five districts $j = 1, 2, ...,5$\n",
    " * Create variables to track the number of Party 1 and Party 2 voters in each district $j = 1, 2, ...,5$\n",
    " * Create binary variables that represent party 1 winning in a given district $j = 1,2, ...,5$. We'll want to maximize the total number of these that are 1 in the solution.\n",
    " * Use the Slide of Tricks to enforce that if the above binary variable is 1, then there must be more Party 1 voters than Party 2 voters in that district\n",
    "\n",
    "(b) Modify your model to maximize the number of party 2 voters who win elections.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solution Problem 2\n",
    "\n",
    "(a) Formulate and solve an integer programming model to help Governer Mander maximize the number of representatives elected from Party 1. Assume that representatives are elected by simple majority, meaning that if a district contains at more Party 1 voters than Party 2 voters, a Party 1 representative is elected."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Total # of party 1 wins: 4.0\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Cbc\n",
    "\n",
    "C = 1:10\n",
    "D = 1:5\n",
    "\n",
    "ρ = Dict(zip(C,[80 60 40 20 40 40 70 50 70 70]))\n",
    "δ = Dict(zip(C,[34 44 44 24 114 64 14 44 54 64]))\n",
    "\n",
    "m = Model(Cbc.Optimizer)\n",
    "\n",
    "@variable(m, x[C,D], Bin)\n",
    "@variable(m, p[D] >= 0)\n",
    "@variable(m, q[D] >= 0)\n",
    "@variable(m, z[D], Bin)\n",
    "\n",
    "@objective(m, Max, sum(z))\n",
    "\n",
    "@constraint(m, tot_p1[j in D], sum(ρ[i]*x[i,j] for i in C) == p[j])\n",
    "@constraint(m, tot_p2[j in D], sum(δ[i]*x[i,j] for i in C) == q[j])\n",
    "@constraint(m, assign[i in C], sum(x[i,j] for j in D) == 1)\n",
    "@constraint(m, min_vote[j in D], p[j] + q[j] >= 150)\n",
    "@constraint(m, max_vote[j in D], p[j] + q[j] <= 250)\n",
    "@constraint(m ,trix[j in D], p[j] - q[j] + (-250-1)z[j] >= -250)\n",
    "\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "println(\"Total # of party 1 wins: \", sum(value.(z)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Total # of party 2 wins: 3.0\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Cbc\n",
    "\n",
    "C = 1:10\n",
    "D = 1:5\n",
    "\n",
    "ρ = Dict(zip(C,[80 60 40 20 40 40 70 50 70 70]))\n",
    "δ = Dict(zip(C,[34 44 44 24 114 64 14 44 54 64]))\n",
    "\n",
    "m = Model(Cbc.Optimizer)\n",
    "\n",
    "@variable(m, x[C,D], Bin)\n",
    "@variable(m, p[D] >= 0)\n",
    "@variable(m, q[D] >= 0)\n",
    "@variable(m, y[D], Bin)\n",
    "\n",
    "@objective(m, Max, sum(y))\n",
    "\n",
    "@constraint(m, tot_p1[j in D], sum(ρ[i]*x[i,j] for i in C) == p[j])\n",
    "@constraint(m, tot_p2[j in D], sum(δ[i]*x[i,j] for i in C) == q[j])\n",
    "@constraint(m, assign[i in C], sum(x[i,j] for j in D) == 1)\n",
    "@constraint(m, min_vote[j in D], p[j] + q[j] >= 150)\n",
    "@constraint(m, max_vote[j in D], p[j] + q[j] <= 250)\n",
    "@constraint(m ,trix[j in D], q[j] - p[j] + (-250-1)y[j] >= -250)\n",
    "\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "println(\"Total # of party 2 wins: \", sum(value.(y)))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
